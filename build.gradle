subprojects {

	apply plugin: 'java'
	apply plugin: 'eclipse'
	apply plugin: 'maven'

	group = 'net.tribe7.jgl'
	version = '0.02'
	sourceCompatibility = 1.6

	ext {
		osName = System.getProperty("os.name").toLowerCase()
		osArch = System.getProperty("os.arch").toLowerCase()

		isWindows = osName.contains("windows")
		isLinux = osName.contains("linux")
		is32bit = osArch.contains("86")
		is64bit = osArch.contains("64")

		unsupportedOs = "Unsupported OS :("
	}

	repositories {
		mavenCentral()
		maven { url "https://raw.github.com/jjzazuet/mvn-repo/master/repo" }
	}

	configurations { provided }

	jar {
		manifest.attributes provider: 'tribe7.net'
		manifest.attributes permissions: 'all-permissions'
	}

	sourceSets {
		main.compileClasspath += configurations.provided
		test.compileClasspath += configurations.provided
		test.runtimeClasspath += configurations.provided
	}

	dependencies {	
		testCompile 'junit:junit:4.8.2'
		compile 'net.tribe7.seeds:seeds-base:14.0.1'
	}

	eclipse {
		classpath {
			plusConfigurations += configurations.provided
			noExportConfigurations += configurations.provided
		}
	}

	task sourcesJar(type: Jar, dependsOn:classes) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}

	task javadocJar(type: Jar, dependsOn:javadoc) {
		classifier = 'javadoc'
		from javadoc.destinationDir
	}

	artifacts {
		archives sourcesJar
		archives javadocJar
	}
}
